plugins {
    id 'com.google.protobuf' version '0.9.4'
    id 'maven-publish'
}

description 'Tronj gRPC client classes'

def grpcVersion = '1.65.1'
def protobufVersion = '4.27.3'

dependencies {
    implementation 'com.google.guava:guava:33.2.1-jre'

    implementation project(':utils')
    implementation project(':abi')

    implementation "io.grpc:grpc-netty-shaded:$grpcVersion"
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
    //json parsing
    implementation "com.google.protobuf:protobuf-java-util:$protobufVersion"

    implementation "org.bouncycastle:bcprov-jdk15on:$bouncycastleVersion"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc { }
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'org.tron.tronj'
            artifactId 'client'
            version '0.1.3'
        }
    }
}
